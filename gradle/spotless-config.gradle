apply plugin: 'com.diffplug.spotless'
String[] copiedJavaFiles = [
        "AtomicThrowable",
        "AutoDisposableHelper",
        "AutoDisposeBackpressureHelper",
        "AutoDisposeEndConsumerHelper",
        "AutoSubscriptionHelper",
        "ExceptionHelper",
        "HalfSerializer",
        "copyright",
].collect { "**/*${it}.java" } as String[]
String[] copiedKotlinFiles = [
        "dependencies",
        "copyright",
        "WeakHashSet",
        "KotterKnife"
].collect { "**/*${it}.kt" } as String[]
spotless {
    format 'misc', {
        target '**/*.md', '**/.gitignore'
        targetExclude "**/.gradle/**"
        indentWithTabs()
        trimTrailingWhitespace()
        endWithNewline()
    }
    kotlin {
        target "**/src/**/*.kt", "**/src/**/*.kt"
        targetExclude '**/spotless/*.kt'
        targetExclude(copiedKotlinFiles)
        ktlint("0.41.0").userData([android: "true"])
        trimTrailingWhitespace()
        endWithNewline()
    }
    kotlinGradle {
        target '**/*.gradle.kts'
        ktlint("0.41.0")
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        target "**/src/**/*.java"
        targetExclude '**/spotless/*.java'
        targetExclude(copiedJavaFiles)
        googleJavaFormat("1.10.0")
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target "**/*.gradle"
        trimTrailingWhitespace()
        endWithNewline()
    }
}
